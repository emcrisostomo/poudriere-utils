#!@ZSH_PATH@
# -*- coding: utf-8; tab-width: 2; indent-tabs-mode: nil; sh-basic-offset: 2; sh-indentation: 2; -*- vim:fenc=utf-8:et:sw=2:ts=2:sts=2
#
# Copyright (C) 2015 Enrico M. Crisostomo
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
#   1. Redistributions of source code must retain the above copyright notice,
#      this list of conditions and the following disclaimer.
#
#   2. Redistributions in binary form must reproduce the above copyright notice,
#      this list of conditions and the following disclaimer in the documentation
#      and/or other materials provided with the distribution.
#
#   3. Neither the name of the copyright holder nor the names of its
#      contributors may be used to endorse or promote products derived from this
#      software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# Unauthorized copy and distribution of this file is strictly prohibited.
#
setopt   local_options
setopt   local_traps
unsetopt glob_subst

set -o errexit
set -o nounset

# Check for required programs.
command -v poudriere > /dev/null 2>&1 || {
  >&2 print -- Cannot find poudriere.
  exit 1
}

# Setting variable defaults.
PROGNAME=$0
: ${POUDRIERE_PATH=@POUDRIERE_PATH@}
: ${POUDRIERE_JAIL_NAME=""}
: ${POUDRIERE_ETC="/usr/local/etc/poudriere.d"}
: ${POUDRIERE_ARCH="amd64"}
: ${POUDRIERE_FREEBSD_VERSION=""}
: ${POUDRIERE_UPDATE_JAIL=0}
: ${POUDRIERE_UPDATE_PORTS=0}

print_usage()
{
  print -- "${PROGNAME}"
  print
  print -- "Usage:"
  print -- "${PROGNAME} [option]* file+"
  print
  print -- "Options:"
  print -- "  -a         Use the specified architecture."
  print -- "  -h         Show this help."
  print -- "  -j         Use the specified jail."
  print -- "  -U         Update the jail."
  print -- "  -u         Update the port tree."
  print -- "  -v         Show verbose output."
  print
  print -- "Report bugs to <enrico.m.crisostomo@gmail.com>."
}

parse_opts()
{
  while getopts ":a:hj:Uuv:" opt
  do
    case $opt in
      a) POUDRIERE_ARCH=${OPTARG} ;;
      h)
        print_usage
        exit 0
        ;;
      j) POUDRIERE_JAIL_NAME=${OPTARG} ;;
      U) POUDRIERE_UPDATE_JAIL=1 ;;
      u) POUDRIERE_UPDATE_PORTS=1 ;;
      v) POUDRIERE_FREEBSD_VERSION=${OPTARG} ;;
      \?)
        >&2 print -- Invalid option -${OPTARG}.
        exit 1
        ;;
      :)
        >&2 print -- Missing argument to -${OPTARG}.
        exit 1
        ;;
    esac
  done

  shift $((OPTIND-1))

  # Sanity checks
  (( $# == 0 )) || {
    >&2 print -- Syntax error.
    exit 2
  }
}

set_variables()
{
  if [[ -z ${POUDRIERE_JAIL_NAME} ]]
  then
    POUDRIERE_JAIL_NAME=${POUDRIERE_FREEBSD_VERSION//[!0-9]/}${POUDRIERE_ARCH}
  fi

  : ${POUDRIERE_JAIL_MAKEFILE=${POUDRIERE_ETC}/${POUDRIERE_JAIL_NAME}-make.conf}
  : ${POUDRIERE_PKGLIST=${POUDRIERE_ETC}/${POUDRIERE_JAIL_NAME}-pkglist}
}

check_variables()
{
  [[ ! -z ${POUDRIERE_FREEBSD_VERSION} ]] || {
    >&2 print -- Missing version.
    exit 4
  }

  [[ ! -z ${POUDRIERE_JAIL_NAME} ]] || {
    >&2 print -- Missing jail name.
    exit 4
  }
}

create_pkglist()
{
  if [[ -e ${POUDRIERE_PKGLIST} ]]
  then
    print -- ${POUDRIERE_PKGLIST} already exists: check its contents.
  else
    touch ${POUDRIERE_PKGLIST}
    print -- Package list created: ${POUDRIERE_PKGLIST}.  Populate it with the port names to build in this jail.
  fi
}

create_jail_makefile()
{
  if [[ ! -e ${POUDRIERE_JAIL_MAKEFILE} ]] || ! grep -q WITH_PKG= ${POUDRIERE_JAIL_MAKEFILE}
  then
    echo WITH_PKG=yes >> ${POUDRIERE_JAIL_MAKEFILE}
  fi
}

create_jail()
{
  poudriere jail \
	    -c \
	    -j ${POUDRIERE_JAIL_NAME} \
	    -v ${POUDRIERE_FREEBSD_VERSION} \
	    -a ${POUDRIERE_ARCH}

  if (( ${POUDRIERE_UPDATE_JAIL} > 0 ))
  then
    poudriere jail -u -j ${POUDRIERE_JAIL_NAME}
  fi
}

# Main routine.
parse_opts $*

# Setting file that depend on parsed variables.
set_variables
check_variables

create_pkglist
create_jail_makefile
create_jail
